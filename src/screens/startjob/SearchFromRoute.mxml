<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  width="100%" 
		  height="100%"
		  gap="0"
		  preinitialize="vgroup1_preinitializeHandler(event)"
		  creationComplete="vgroup1_creationCompleteHandler(event)" 
		  xmlns:system="components.system.*" xmlns:components="flatspark.components.*">
	<fx:Script>
		<![CDATA[
			import com.distriqt.extension.dialog.Dialog;
			import com.distriqt.extension.dialog.PickerDialogView;
			import com.distriqt.extension.dialog.builders.PickerDialogBuilder;
			import com.distriqt.extension.dialog.events.DialogViewEvent;
			
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			
			import spark.events.TextOperationEvent;
			
			import classes.Globals;
			import classes.Model.Database;
			
			import events.MainNavEvent;
			import events.Model.QueryEvent;
			
			import flatspark.enums.BrandColorEnum;
			import flatspark.enums.TextInputSizeEnum;

			[Bindable] private var arrSalepoints:ArrayCollection = new ArrayCollection();
			[Bindable] private var rouName:String = "не выбран";
			private var acRoutes:ArrayCollection = new ArrayCollection();
						
			protected function vgroup1_preinitializeHandler(event:FlexEvent):void
			{
				stage.setAspectRatio(StageAspectRatio.PORTRAIT);
			}			
			
			protected function vgroup1_creationCompleteHandler(event:FlexEvent):void
			{
				loadSalepoints("WHERE VIC_DATE = '" + Globals.dateToYYYYMMDDHyphen(new Date()) + " 00:00:00.0'");
			}
			private function loadSalepoints(WHERE_CLAUSE:String):void
			{				
				Database.addEventListener(QueryEvent.DATA_LOADED, arrSalepointsLoaded, false, 0, true);
				Database.init("SELECT DISTINCT " +
										"ROU_CODE, ROU_NAME, SAL_CODE, SAL_NAME, " +
										"SAL_ID, SAL_NOTES, SAL_HOUSE, SAL_OWNER, LOC_NAME, SAL_CHA_CODE " +
										"FROM VIEW_CAL_ROUTE_SALEPOINTS " + WHERE_CLAUSE + " ORDER BY ROS_ORDER");
			}
			private function arrSalepointsLoaded(e:QueryEvent):void
			{
				Database.removeEventListener(QueryEvent.DATA_LOADED, arrSalepointsLoaded );
				arrSalepoints = e.data;		
				
				if (e.data.length > 0) {
					Globals.ROU_CODE = e.data.getItemAt(0).ROU_CODE;
					rouName = e.data.getItemAt(0).ROU_NAME;
				} else {
					Dialog.service.toast("Нет данных для отображения");
				}
			}
			
			protected function txtFilter_changeHandler(event:TextOperationEvent):void
			{
				arrSalepoints.filterFunction = filterSalepoints;
				arrSalepoints.refresh();
			}
			
			private function filterSalepoints(item:Object):Boolean 
			{
				var result:Boolean = false;
				
				var itemSalName:String = "";
				var itemSalOwner:String = "";
				var itemSalHouse:String = "";
				
				var searchString:String = txtFilter.text.toLowerCase();
				
				try {	itemSalName		= 	(item.SAL_NAME as String).toLowerCase();		} catch (err1:Error) {		}
				try {	itemSalOwner 	= 	(item.SAL_OWNER as String).toLowerCase();		} catch (err2:Error) {		}
				try {	itemSalHouse 	= 	(item.SAL_HOUSE as String).toLowerCase();		} catch (err3:Error) {		}
				
				if ((itemSalName.indexOf(searchString) > -1) || (itemSalOwner.indexOf(searchString) > -1) || (itemSalHouse.indexOf(searchString) > -1))
				{
					result = true;
				}
				
				return result;
			}
			
						
			protected function btnChooseRoute_clickHandler(event:MouseEvent):void
			{
				Database.addEventListener(QueryEvent.DATA_LOADED, arrRoutesLoaded, false, 0, true);
				Database.init("SELECT * FROM VIEW_ST_ROUTE");
			}
			private function arrRoutesLoaded(e:QueryEvent):void
			{
				Database.removeEventListener(QueryEvent.DATA_LOADED, arrRoutesLoaded );
				acRoutes = e.data;
				
				var arrRoutes:Array = new Array();				
				for (var i:int=0; i < e.data.length; i++)
				{
					arrRoutes.push(e.data.getItemAt(i).ROU_NAME + " (количество точек: " + e.data.getItemAt(i).SAL_COUNT + ")");
				}
				
				var picker:PickerDialogView = Dialog.service.create(
					new PickerDialogBuilder()
					.setTitle( "Выберите маршрут" )
					.setCancelLabel("Отмена")
					.setAcceptLabel("Выбрать")
					.addColumn(arrRoutes)
					.build()
				);
				
				picker.addEventListener( DialogViewEvent.CLOSED, picker_closedHandler );
				picker.addEventListener( DialogViewEvent.CANCELLED, picker_closedHandler );
				picker.show();
			}

			private function picker_closedHandler( event:DialogViewEvent ):void
			{
				trace( "picker closed ("+event.index+") -> [ " + event.indexes.join(",") + " ] " + event.values.join(",") );
				
				if (event.index == 1) {
					txtFilter.text = "";
					loadSalepoints("WHERE ROU_CODE = " + acRoutes.getItemAt(event.indexes[0]).ROU_CODE);
				}
				
				var picker:PickerDialogView = PickerDialogView(event.currentTarget);
				picker.removeEventListener( DialogViewEvent.CLOSED, picker_closedHandler );
				picker.removeEventListener( DialogViewEvent.CANCELLED, picker_closedHandler );
				picker.dispose();
			}
			
			protected function lstSalepoints_clickHandler(event:MouseEvent):void
			{
				if (lstSalepoints.selectedIndex > -1) {
					Globals.SAL_CODE = lstSalepoints.selectedItem.SAL_CODE;
					Globals.SAL_ID = lstSalepoints.selectedItem.SAL_ID;
					Globals.SAL_NAME = lstSalepoints.selectedItem.SAL_NAME;
					Globals.SAL_CHA_CODE = lstSalepoints.selectedItem.SAL_CHA_CODE;
					
					dispatchEvent(new MainNavEvent(MainNavEvent.NEXT_EVENT,true,false,"901"));
				}
			}
			
			private function handlerFunction():void
			{
				stage.focus = null
			}
			
		]]>
	</fx:Script>
	
	<system:TsaActionBar id="tsaActionBar" titleText="Выбор торговой точки" backVisible="true" />
	<s:VGroup width="100%" height="100%" padding="5">
		<components:ButtonIcon id="btnChooseRoute"
							   label="ВЫБРАТЬ МАРШРУТ"
							   width="100%"
							   height="10%"
							   brand="{BrandColorEnum.Primary}"
							   click="btnChooseRoute_clickHandler(event)" />
		<s:TextInput id="txtFilter" 
					 width="100%" 
					 height="{TextInputSizeEnum.Large}" 
					 enabled="{arrSalepoints.length > 0 ? true : false}" 
					 returnKeyLabel="done"
					 enter="handlerFunction()"
					 prompt="Фильтр по коду, названию или адресу ТТ..." 
					 change="txtFilter_changeHandler(event)" />
		<s:Panel title="Маршрут: {rouName} ({arrSalepoints.length.toString()})" width="100%" height="90%">
			<s:List id="lstSalepoints" width="100%" height="100%"
					dataProvider="{arrSalepoints}"
					labelField="SAL_NAME" 
					itemRenderer="renderers.SalepointsRenderer"
					click="lstSalepoints_clickHandler(event)">
			</s:List>
		</s:Panel>
	</s:VGroup>	
	
</s:VGroup>
